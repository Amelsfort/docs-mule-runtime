= Set a Default Value
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]
:keywords: studio, anypoint, transform, transformer, format, aggregate, rename, split, filter convert, xml, json, csv, pojo, java object, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping, examples

You can define default values to use in case any value you are querying is not present.

Set default values by any of the following methods:

* Using the `default` keyword.
* Setting the default in an `if-else` or `else-if` statement.
* Using `else` when pattern matching.

//LINK TO DW 1.0 EXAMPLES:
include::partial$dataweave1-links.adoc[tag=dataweave1Examples]

== Using the Key Word default

Consider a Mule application that expects a JSON input with fields `id` and `name` to do a transformation. +
You can configure default values for these fields in case the payload value is `null` for example:

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
{
  "userId": payload.id default "0000",
  "userName": payload.name default "Undefined"
}
----

* If the Mule Application receives the following JSON input:
+
[source,json, linenums]
----
{
  "id": "123",
  "name": "Max the Mule"
}
----
+
The DataWeave transformation output is:
+
[source,JSON,linenums]
----
{
  "userId": "123",
  "userName": "Max the Mule"
}
----

* If the Mule Application receives an empty JSON message, the DataWeave transformation output is:
+
[source,JSON,linenums]
----
{
  "userId": "0000",
  "userName": "Undefined"
}
----

== Setting a Default Within if-else and else-if Statements

You can use
xref:dataweave-flow-control.adoc#control_flow_if_else[if-else]
and
xref:dataweave-flow-control.adoc#control_flow_else_if[else-if]
statements to specify a default value.

The following example sets `US` as the default value for `userLocation` if it is not present in the JSON input message:

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
if (payload.location != null) {
	"userLocation" : payload.location
} else {
	"userLocation" : "United States"
}
----

* If the Mule Application receives the following JSON input:
+
[source,json, linenums]
----
{
  "location": "Argentina"
}
----
+
The DataWeave transformation output is:
+
[source,JSON,linenums]
----
{
  "userLocation": "Argentina"
}
----

* If the Mule Application receives an empty JSON message, the DataWeave transformation output is:
+
[source,JSON,linenums]
----
{
  "userLocation": "United States"
}
----

== Setting a Default When Matching Patterns

In xref:dataweave-pattern-matching.adoc[pattern-matching scripts], DataWeave
`case` statements end with an `else` expression that can serve as the default
to return if all preceding `case` expressions return `false`.

.DataWeave Script:
[source,DataWeave, linenums]
----
%dw 2.0
var myVar = "someString"
output application/json
---
myVar match {
    case myVarOne if (myVar == "some") -> ("some" ++ "is myVar")
    case myVarOne if (myVar == "strings") -> ("strings" ++ "is myVar")
    else -> myVar ++ " is myVar"
}
----

.Output:
[source,JSON,linenums]
----
"someString is myVar"
----
