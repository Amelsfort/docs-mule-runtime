= Add and Subtract Time
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]
:keywords: studio, anypoint, transform, transformer, format, xml, json, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping

This DataWeave example shows multiple addition and subtraction operations that
deal with date and time types.
//LINK TO DW 1.0 EXAMPLES:
include::partial$dataweave1-links.adoc[tag=dataweave1Examples]

The examples use:

* `now` function to output the current date and time
* `as` function to coerce a String to Period type
* `P<date>T<time>` for the Period data type, which provides designators for years, months, days, hours, minutes, and seconds +
For example, `|P2Y9M1D|` refers to a period of two years, nine months, and one day, and `|PT5H4M3S|` indicates a time period of five hours, four minutes, and three seconds.

.DataWeave
[source,dataweave,linenums]
----
%dw 2.0
output application/json
var numberOfDays = 3
---
{
  yesterday: now() - |P1D|,
  theOtherDay: now() - ("P$(numberOfDays)D" as Period),
  a: |2003-10-01| - |P1Y|,
  b: |P1Y| - |2003-10-01|,
  c: |2003-10-01T23:57:59Z| - |P1Y|,
  d: |2003-10-01T23:57:59Z| + |P1Y|,
  e: |2003-10-01T23:57:59| - |P1Y|,
  f: |PT9M| - |23:59:56|, 
  g: |23:59:56| + |PT9M|,
  h: |23:59:56-03:00| - |PT9M|,
  u: |23:59:56-03:00| - |22:59:56-03:00|,
  j: |23:59:56-03:00| - |22:59:56-00:00|,
  k: |2003-10-01T23:57:59| - |P2Y9M1D| - |PT57M59S| + |PT2H|,
  l: |23:59:56| - |22:59:56|,
  o: |2003-10-01| - |2002-09-23|,
  p: |2003-10-01T23:57:59Z| - |2002-10-01T23:57:59Z|,
  q: |2003-10-01T23:57:59| - |2002-10-01T23:57:59|
}
----

.Output
[source,json,linenums]
----
{
  "yesterday": "2020-09-16T10:20:44.532-07:00",
  "theOtherDay": "2020-09-14T10:20:44.532-07:00",
  "a": "2002-10-01",
  "b": "2002-10-01",
  "c": "2002-10-01T23:57:59Z",
  "d": "2004-10-01T23:57:59Z",
  "e": "2002-10-01T23:57:59",
  "f": "23:50:56",
  "g": "00:08:56",
  "h": "23:50:56-03:00",
  "u": "PT1H",
  "j": "PT4H",
  "k": "2001-01-01T01:00:00",
  "l": "PT1H",
  "o": "PT8952H",
  "p": "PT8760H",
  "q": "PT8760H"
}
----

== See Also

* xref:dataweave-types.adoc#dw_type_dates_period[Period]

* xref:dataweave-cookbook-parse-dates.adoc[Parse Dates]

* xref:dataweave-types.adoc[DataWeave Types]

* xref:dataweave-selectors.adoc[DataWeave Selectors]

* xref:dataweave-cookbook.adoc[DataWeave Cookbook]
