= Look Up Data in a CSV File
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]

This DataWeave example uses the `filter` function to return the country code stored in a CSV file to the specified input phone numbers.
//LINK TO DW 1.0 EXAMPLES:
include::partial$dataweave1-links.adoc[tag=dataweave1Examples]

Given the following JSON input phone numbers as the payload:

[source,JSON,linenums]
----
[
   "54-112724555",
   "1-6298765432"
]
----

And the CSV file in a var called `country_codes` with the following values:

[source,CSV,linenums]
----
CALLING_CODE,COUNTRY_CODE
1,US
7,RU
54,AR
20,EG
32,BE
33,FR
505,NI
506,CR
1876,JM
1905,CA
1939,PR
262262,RE
262269,YT
.,.
.,.
----

The following DataWeave script reads the input payload phone numbers, and uses the function `lookupCountryCode` to search the country code in the variable `country_code` Subsequently, uses the `filter` function to match the country code to the phone number, and returns the filtered results in an array of JSON objects:

//MANUAL TESTING REQUIRED FOR NOW (NEED TO TEST WITH XLSX FILE)
.DataWeave Script:
[source,dataweave, linenums]
----
%dw 2.0
output application/json

fun lookupCountryCode(phoneNumber: String): String = do {
    var callingCode = (phoneNumber splitBy("-"))[0]
    --- 
    (vars.country_code filter ((item, index) -> item.CALLING_CODE == callingCode) map ((item, index) -> item.COUNTRY_CODE))[0]
}
---
payload map ((item, index) -> { phone: item, countryCode: lookupCountryCode(item)})
----

//MANUAL TESTING REQUIRED (FOR NOW)
.Sample Output (JSON):
[source,JSON,linenums]
----
[
  {
    "phone": "54-112724555",
    "countryCode": "AR"
  },
  {
    "phone": "1-6298765432",
    "countryCode": "US"
  }
]
----

== See Also

* xref:dataweave-formats.adoc[Supported Data Formats]
