= Flow Control and Scope Operations in DataWeave
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]

You can use the following operators within any DataWeave expression:

* <<control_flow_do,`do`>>
* <<control_flow_if_else,`if else`>>
* <<control_flow_else_if,`else if`>>

//LINK TO DW 1.0 LANDING PAGE IN MULE 3.9 DOCS:
include::partial$dataweave1-links.adoc[tag=dataweave1LandingPage]

[[control_flow_do]]
== `do`

A `do` statement creates a scope in which new variables, functions, annotations, or namespaces can be declared and used. The syntax is similar to a mapping in that it is composed of a header and body separated by `&#45;&#45;&#45;`. Its header is where all the declarations are defined, and its body is the result of the expression.

This example uses `do` to return the string `"DataWeave"` when `myfun()` is called from the main body of the script.

.DataWeave Script:
[source,dataweave,linenums]
----
%dw 2.0
output application/json
fun myfun() = do {
    var name = "DataWeave"
    ---
    name
}
---
{ result: myfun() }
----

This example uses `do` to return the string `"DataWeave"` when the variable `myVar` is referenced from the main body of the script.

.DataWeave Script:
[source,dataweave,linenums]
----
%dw 2.0
output application/json
var myVar = do {
    var name = "DataWeave"
    ---
    name
}
---
{ result: myVar }
----

Both scripts produce this output:

.Output:
[source,json,linenums]
----
{
  "result": "DataWeave"
}
----

The next example uses `do` to prepend the string `"Foo"` to a string (`" Bar"`) that is passed to the `test(p: String)` function.

.DataWeave Script:
[source,dataweave,linenums]
----
%dw 2.0
output application/json
fun test(p: String) = do {
    var a = "Foo" ++ p
    ---
    a
}
---
{ result: test(" Bar") }
----

.Output:
[source,json,linenums]
----
{
  "result": "Foo Bar"
}
----

See also, xref:dataweave-variables.adoc#example_local_variable[Examples: Local DataWeave Variables].

[[control_flow_if_else]]
== `if else`

An `if` statement evaluates a conditional expression and returns the value under the `if` only if the conditional expression is true. Otherwise, it will return the expression under `else`. Every `if` expression must have a matching `else` expression.

The following example uses the input `{ country : "FRANCE" }`, which is defined by the `myVar` variable in the header:

.DataWeave Script:
[source,dataweave,linenums]
----
%dw 2.0
var myVar = { country : "FRANCE" }
output application/json
---
if (myVar.country == "USA")
  { currency: "USD" }
else { currency: "EUR" }
----

Note that you can use `if else` statements on any conditions that evaluate to `true` or `false`, including mathematical, logical, equality, and relational
conditions. For example, you can replace `if (myVar.country == "USA")` in the script with `if ( 1 + 1 >= 2)`. 

.Output:
----
{
  "currency": "EUR"
}
----


An `if else` statement can act on any valid DataWeave input and return the data in any valid output format. For example, the following script transforms the input to XML and uses `if else` statements within the following:

* A `using` statement that accompanies a `map` operation on the `firstInput` value
* A mathematical operation in `ex1` that can return `true` or `false`
* An equality operation in `ex2` that checks the index of an array and returns a string if the result is not `1`
* An operation in `ex3` that either returns the `id` defined by the `using` statement or `string`
* A check in `ex4` that returns the string `"not null"` or `"null value"` depending on whether the value of the `id` is `null`


.DataWeave Script:
[source,dataweave,linenums]
----
%dw 2.0
var firstInput = [
  { "bookId":"101",
    "title":"world history",
    "price":"19.99"
  },
  {
    "bookId":"202",
    "title":"the great outdoors",
    "price":"15.99"
  }
]
output application/xml 
---
xmlRoot: { xmlChild : 
	firstInput map (value) -> using (id = (if (value.bookId as Number == 202) "the book ID really is 202" else value.bookId as Number))
	{
        ex1 : if (1 + 1 == 55) true else false,
        ex2 : if ([1,2,3,4][1] == 1) 1 else "value of index 1 is not 1",
        ex3 : if (id == 101) id as Number else "greater than 101",
        ex4 : if (id != null) "not null" else "null value" 
    }
}
----

.Output:
----
<?xml version='1.0' encoding='UTF-8'?>
<xmlRoot>
  <xmlChild>
    <ex1>false</ex1>
    <ex2>value of index 1 is not 1</ex2>
    <ex3>101</ex3>
    <ex4>not null</ex4>
  </xmlChild>
  <xmlChild>
    <ex1>false</ex1>
    <ex2>value of index 1 is not 1</ex2>
    <ex3>greater than 101</ex3>
    <ex4>not null</ex4>
  </xmlChild>
</xmlRoot>
----

Additional examples are available in xref:dw-operators.adoc[DataWeave Operators].

[[control_flow_else_if]]
== `else if`

You can chain several `else` expressions together within an if-else construct by incorporating `else if`.
The following example uses the input `var myVar = { country : "UK" }`, which is defined by the `myVar` variable in the header.

.DataWeave Script:
[source,dataweave,linenums]
----
%dw 2.0
var myVar = { country : "UK" }
output application/json
---
if (myVar.country =="USA")
	{ currency: "USD" }
else if (myVar.country =="UK")
	{ currency: "GBP" }
else { currency: "EUR" }
----

.Output
----
{
  "currency": "GBP"
}
----

== See Also

* xref:dataweave-types.adoc[DataWeave Types]
* xref:dataweave-selectors.adoc[DataWeave Selectors]
